class g:View

  def initialize(name, params={})
    self.name = name
    attrs = {
          \ '_original_size': [0, 0],
          \ 'minimized': 0,
          \ 'minimized_size': 2,
          \ 'minimize_on_toggle': get(g:, 'maque_tmux_minimize_on_toggle', 0),
          \ 'focus_on_restore': 0,
          \ 'vertical': 1,
          \ 'size': 0,
          \ }
    extend(attrs, params)
    attrs.minimized_size = max([attrs.minimized_size, 2])
    extend(viewObj, attrs)
  end
    
  defm toggle
    if self.open()
      if self.minimize_on_toggle
        self.toggle_minimized()
      else
        self.close()
      end
    else
      self.create()
    end
  end

  defm toggle_minimized
    if self.minimized
      self.restore()
    else
      self.minimize()
    end
  end

  " Store the current size and set the minimized_size.
  " Do not use the 'size' parameter for restoring, as the pane could have been
  " altered purposefully.
  defm minimize
    if self.open() && !self.minimized
      self._original_size = self.current_size()
      self.apply_size(self.minimized_size) unless self.in_layout()
      self.minimized = 1
      self.pack_layout()
    end
  end

  defm restore
    if self.open() && self.minimized
      self.resize(self._original_size[0], self._original_size[1]) unless self.in_layout()
      self.minimized = 0
      self.focus() if self.focus_on_restore
      self.pack_layout()
    end
  end

  defm apply_size(size)
    if self._vertical()
      self.resize(0, size)
    else
      self.resize(size, 0)
    end
  end

  defm _vertical
    if self.in_layout()
      return self.layout.layout_vertical()
    else
      return self.vertical
    end
  end

  defm fixed_size
    return self.size != 0 || self.minimized
  end

  defm effective_size
    return self.minimized ? self.minimized_size : self.size
  end

  defm layout_size
    return self.current_size()[self._vertical()]
  end

  defm layout_position
    return self.current_position()[self._vertical()]
  end

  defm pack_layout
    self.layout.pack() if self.in_layout()
    self.pack()
  end

  defm pack
  end
end
