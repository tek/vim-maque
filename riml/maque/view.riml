class g:View

  def initialize(name, params={})
    self.name = name
    attrs = {
          \ '_original_size': [0, 0],
          \ 'minimized': 0,
          \ 'minimized_size': 2,
          \ 'minimize_on_toggle': get(g:, 'maque_tmux_minimize_on_toggle', 0),
          \ 'focus_on_restore': 0,
          \ 'vertical': 1,
          \ }
    extend(attrs, params)
    attrs.minimized_size = max([attrs.minimized_size, 2])
    extend(viewObj, attrs)
  end
    
  defm toggle
    if self.open()
      if self.minimize_on_toggle
        self.toggle_minimized()
      else
        self.close()
      end
    else
      self.create()
    end
  end

  defm toggle_minimized
    if self.minimized
      self.restore()
    else
      self.minimize()
    end
  end

  " Store the current size and set the minimized_size.
  " Do not use the 'size' parameter for restoring, as the pane could have been
  " altered purposefully.
  defm minimize
    if self.open() && !self.minimized
      self._original_size = self.current_size()
      self._apply_size(self.minimized_size)
      self.minimized = 1
    end
  end

  defm restore
    if self.open() && self.minimized
      self.resize(self._original_size[0], self._original_size[1])
      self.minimized = 0
      self.focus() if self.focus_on_restore
    end
  end

  defm _apply_size(size)
    if self._vertical()
      self.resize(self._original_size[0], size)
    else
      self.resize(size, self._original_size[1])
    end
  end

  defm _vertical
    if self.in_layout()
      return self.layout.direction == 'vertical'
    else
      return self.vertical
    end
  end
end
